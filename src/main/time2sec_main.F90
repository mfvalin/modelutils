subroutine time2sec_main()
  use timestr_mod, only: timestr2sec
  use str_mod, only: str_toreal
  implicit none
  !  Argument-free interface for time2sec time string converter
#include <rmnlib_basics.hf>

  !  Internal parameterrs
  integer, parameter :: STDOUT=6,STDERR=0,STRLEN=1024

  !  CCARD preparation
  integer, parameter :: NARGS=2
  character(len=STRLEN), dimension(NARGS) :: &
       arglist =(/'time.'    ,'dt.  '/), &
       def     =(/'UNDEFINED','1.       '/), &
       val     =(/'UNDEFINED','1.       '/)

  !  Internal variables
  integer :: npos,istat
  real :: sec,dt
  real(RDOUBLE) :: dt_8
  character(len=STRLEN) :: timestr,dtstr

  !  Obtain arguments
  call ccard(arglist,def,val,NARGS,npos)
  timestr = val(1)
  dtstr   = val(2)
  
  !  Enforce mandatory arguments
  if (timestr == def(1)) then
     write(STDERR,*) "Error: a time string (-time) must be provided "//trim(timestr)
     return
  endif

  istat = str_toreal(dt,dtstr)
  if (.not.RMN_IS_OK(istat)) then
     write(STDERR,*) "Error: invalid number provided for delta_T (-dt): "//trim(dtstr)
     return
  endif
  dt_8 = dt

  !  Call library function to retrieve number of seconds from time string
  istat = timestr2sec(sec,timestr,dt_8)
  if (.not.RMN_IS_OK(istat)) then
     write(STDERR,*) "Error: unable to convert date string "//trim(timestr)
     return
  else
     write(STDOUT,*) sec
  endif
  
  return
end subroutine time2sec_main
